--[REGION 'HEADER']
-- -----------------------------------------------------------------------------
-- title      :  Wks Databases Restore
-- description:  restore .bak files in a dir to the local sql server
--               if the database already exists it will be overwritten
--               if the database does not exist it will be created
--               to enable xp_cmdshell: rightclick server > Facets > Surface Area Configuration > XPCmshellEnbled > true
--               or better run this:
/*
-- to allow advanced options to be changed
EXEC sp_configure 'show advanced options', 1
GO
-- to update the currently configured value for advanced options
RECONFIGURE
GO
-- to enable the feature
EXEC sp_configure 'xp_cmdshell', 1
GO
-- to update the currently configured value for this feature
RECONFIGURE
GO
*/
-- author     :  giarussi
-- created    :  10/27/2022 02:50:50 PM
-- copyright  :  (c) Wks 2022
-- licence    :  BDS
-- -----------------------------------------------------------------------------
--[ENDREGION]

-- vars
declare @kp varchar(1024); set @kp = 'C:\Program Files\Microsoft SQL Server\MSSQL16.MSSQLSERVER\MSSQL\Backup' -- backuppath
declare @rp varchar(1024); set @rp = 'C:\Program Files\Microsoft SQL Server\MSSQL16.MSSQLSERVER\MSSQL\DATA'   -- restorepath
declare @t1 table(FldFileBak varchar(max))                                                                    -- filesbak
declare @fb varchar(1024)                                                                                     -- filebak
declare @db varchar(1024)                                                                                     -- dbaname
declare @cm varchar(1024)                                                                                     -- command

-- filespec
set @cm = 'dir /B "' + @kp + '\*.bak"'
--print @cm

-- t1
insert into @t1 exec master.sys.xp_cmdshell @cm
delete @t1 where FldFileBak is null
--select * from @t1

-- cursor
declare @c cursor   
set @c = cursor for select FldFileBak from @t1 /*where FldFileBak not in ('DbaPlace.bak')*/ order by 1
open @c
fetch next from @c into @fb
while @@fetch_status = 0 begin
    -- zip
    set @db = replace(@fb, '.bak', '')

    -- command
    set @cm = '
USE [master]
ALTER   DATABASE [' + @db + '] SET SINGLE_USER WITH ROLLBACK IMMEDIATE
RESTORE DATABASE [' + @db + '] FROM DISK = N''' + @kp + '\' + @fb + ''' WITH FILE = 1, MOVE N''' + @db     + ''' TO N''' + @rp + '\' + @db + '.mdf'', MOVE N''' + @db + '_log'' TO N''' + @rp + '\' + @db + '.ldf'', NOUNLOAD, REPLACE, STATS=5
ALTER   DATABASE [' + @db + '] SET MULTI_USER
--GO
'
    -- go
    print @cm
    exec(@cm)
    --execute sp_executesql @cm

    -- next
    fetch next from @c into @fb
end
